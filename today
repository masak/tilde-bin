#! /usr/local/bin/cautious-perl6

sub MAIN(:$force) {
    my $today = Date.today;
    unless $force {
        my $LAST_RUN_FILE = "%*ENV<HOME>/.today-last-run";
        exit
            if try slurp($LAST_RUN_FILE) eq $today;
        spurt($LAST_RUN_FILE, ~$today);
    }

    my %year-events;
    try "%*ENV<HOME>/.today-data".IO.lines.map({
        /^ (\d+) \h+ (\d+) \h+ (\N+) $/
            or die "Unrecognized line in data file: '$_'";
        %year-events{"month $0, day $1"} = ~$2;
    });

    for Date.today .. Date.today.later(:7days) -> $date {
        my $events = %year-events{"month $date.month(), day $date.day()"} // "";
        if $date.day-of-month == 1 {
            $events ~= ", time to send hours to \$boss";
        }
        my $wd = <. mon tue wed thu fri sat sun>[$date.day-of-week];
        say "  $wd  {$date.day-of-month}  $events";
    }
}
